### A. 

```C

/* Bubble sort: Array version */
void bubble_a(long *data, long count) {
    long *curr;
    long *last;
    for (last = data + (count - 1); last >= data; last--) {
        for (curr = data; curr < last; curr++)
            if (*(curr + 1) < *curr) {
                /* Swap adjacent elements */
                long t = *(curr + 1);
                *(curr + 1) = *curr;
                *curr = t;
            }
    }
}
```

### B.

Compiling the above C code on my M1 machine:

```
> clang -o bubble.o -c -O1 -arch x86_64 bubble.c
> objdump -d bubble.o

bubble.o:    file format mach-o 64-bit x86-64

Disassembly of section __TEXT,__text:

0000000000000000 <_bubble_a>:
       0: 55                               pushq    %rbp
       1: 48 89 e5                         movq    %rsp, %rbp
       4: 48 85 f6                         testq    %rsi, %rsi
       7: 7e 58                            jle    0x61 <_bubble_a+0x61>
       9: 4c 8d 04 f7                      leaq    (%rdi,%rsi,8), %r8
       d: 49 83 c0 f8                      addq    $-8, %r8
      11: eb 16                            jmp    0x29 <_bubble_a+0x29>
      13: 66 66 66 66 2e 0f 1f 84 00 00 00 00 00           nopw    %cs:(%rax,%rax)
      20: 49 83 c0 f8                      addq    $-8, %r8
      24: 49 39 f8                         cmpq    %rdi, %r8
      27: 72 38                            jb    0x61 <_bubble_a+0x61>
      29: 49 39 f8                         cmpq    %rdi, %r8
      2c: 76 f2                            jbe    0x20 <_bubble_a+0x20>
      2e: 48 89 f9                         movq    %rdi, %rcx
      31: eb 15                            jmp    0x48 <_bubble_a+0x48>
      33: 66 66 66 66 2e 0f 1f 84 00 00 00 00 00           nopw    %cs:(%rax,%rax)
      40: 48 89 d1                         movq    %rdx, %rcx
      43: 4c 39 c2                         cmpq    %r8, %rdx
      46: 73 d8                            jae    0x20 <_bubble_a+0x20>
      48: 48 8d 51 08                      leaq    8(%rcx), %rdx
      4c: 48 8b 01                         movq    (%rcx), %rax
      4f: 48 8b 71 08                      movq    8(%rcx), %rsi
      53: 48 39 c6                         cmpq    %rax, %rsi
      56: 7d e8                            jge    0x40 <_bubble_a+0x40>
      58: 48 89 41 08                      movq    %rax, 8(%rcx)
      5c: 48 89 31                         movq    %rsi, (%rcx)
      5f: eb df                            jmp    0x40 <_bubble_a+0x40>
      61: 5d                               popq    %rbp
      62: c3                               retq
```

Analysis:
```
# void bubble_a(long *data, long count) 
#   data in %rdi; count in %rsi
 0: 55                               pushq    %rbp
 1: 48 89 e5                         movq    %rsp, %rbp
 4: 48 85 f6                         testq    %rsi, %rsi                         # if count == 0
 7: 7e 58                            jle    0x61 <_bubble_a+0x61>                #   return
 9: 4c 8d 04 f7                      leaq    (%rdi,%rsi,8), %r8                  # last = data + count (%r8 = last)
 d: 49 83 c0 f8                      addq    $-8, %r8                            # last = last - 1
11: eb 16                            jmp    0x29 <_bubble_a+0x29>                # go to outer_loop

13: 66 66 66 66 2e 0f 1f 84 00 00 00 00 00           nopw    %cs:(%rax,%rax)     # <compiler garbage>

                                outer_loop_next:
20: 49 83 c0 f8                      addq $-8, %r8                               # last = last - 1
24: 49 39 f8                         cmpq %rdi, %r8                              # compare last with curr
27: 72 38                            jb    0x61 <_bubble_a+0x61>                 # if last < data then return (outer loop condition: last >= data)
                                outer_loop:
29: 49 39 f8                         cmpq %rdi, %r8                              # compare last with curr (curr == data at this point)
2c: 76 f2                            jbe    0x20 <_bubble_a+0x20>                # if last <= curr then go to outer_loop_next
                                                                                 # this actually means the inner loop condition (curr < last) is no longer true
2e: 48 89 f9                         movq %rdi, %rcx                             # curr = data   (%rcx = data)
31: eb 15                            jmp    0x48 <_bubble_a+0x48>                # go to inner_loop

33: 66 66 66 66 2e 0f 1f 84 00 00 00 00 00          nopw    %cs:(%rax,%rax)      # <compiler garbage>

                                inner_loop_next:
40: 48 89 d1                         movq    %rdx, %rcx                          # curr = curr++    (%rcx is later increased)
43: 4c 39 c2                         cmpq    %r8, %rdx                           # compare curr with last
46: 73 d8                            jae    0x20 <_bubble_a+0x20>                # if curr >= last go to outer_loop_next
                                inner_loop:
48: 48 8d 51 08                      leaq    8(%rcx), %rdx                       # %rdx = curr + 1  (curr++)
4c: 48 8b 01                         movq    (%rcx), %rax                        # %rax = *curr
4f: 48 8b 71 08                      movq    8(%rcx), %rsi                       # t = *(curr + 1)
53: 48 39 c6                         cmpq    %rax, %rsi                          # compare *(curr + 1) with *curr
56: 7d e8                            jge     0x40 <_bubble_a+0x40>               # if *(curr + 1) >= *curr go to inner_loop_next
58: 48 89 41 08                      movq    %rax, 8(%rcx)                       # *(curr + 1) = *curr
5c: 48 89 31                         movq    %rsi, (%rcx)                        # *curr = t
5f: eb df                            jmp     0x40 <_bubble_a+0x40>               # go to inner_loop_next
61: 5d                               popq    %rbp
62: c3                               retq
```      

Translating to Y86-64:
```
# Execution begins at address 0 
    .pos 0
    irmovq stack, %rsp  # Set up stack pointer
    call main           # Execute main program
    halt                # Terminate program

# Array of 4 elements
    .align 8
array:    .quad 0x000000000004
    .quad 0x000000000002
    .quad 0x000000000001
    .quad 0x000000000003

main:
    irmovq array, %rdi
    irmovq $4, %rsi
    call bubble_a        # bubble_a(array, 4)
    ret

# void bubble_a(long *data, long count)
# data in %rdi, count in %rsi
bubble_a:
    pushq    %rbp
    rrmovq    %rsp, %rbp
    irmovq  $8, %r10
    andq    %rsi, %rsi
    jle    done
    rrmovq %rsi, %r8
    addq %r8, %r8               # %r8 = 2*count
    addq %r8, %r8               # %r8 = 4*count
    addq %r8, %r8               # %r8 = 8*count
    addq %rdi, %r8              # %r8 = data + count
    subq %r10, %r8                # last = data + (count - 1)
    jmp    outer_loop           # go to outer_loop
outer_loop_next:
    subq    %r10, %r8             # last = last - 1
    rrmovq  %r8, %r9
    subq    %rdi, %r9           # compare last with data
    jl    done                  # if last < data then return (outer loop condition: last >= data)
outer_loop:
    rrmovq  %r8, %r9
    subq    %rdi, %r9           # compare last with curr (curr == data at this point)
    jle    outer_loop_next      # if last <= curr then go to outer_loop_next (inner loop condition: curr < last)
    rrmovq    %rdi, %rcx        # curr = data   (%rcx = data)
    jmp    inner_loop           # go to inner_loop
inner_loop_next:
    rrmovq    %rdx, %rcx        # curr = curr++
    rrmovq  %rdx, %r9
    subq    %r8, %r9            # compare curr with last
    jge    outer_loop_next      # if curr >= last go to outer_loop_next
inner_loop:
    rrmovq  %rcx, %rdx
    addq    %r10, %rdx            # %rdx = curr + 1
    mrmovq    (%rcx), %rax      # %rax = *curr
    mrmovq    8(%rcx), %rsi     # t = *(curr + 1)
    rrmovq  %rsi, %r9
    subq    %rax, %r9           # compare *(curr + 1) with *curr
    jge    inner_loop_next      # if *(curr + 1) >= *curr go to inner_loop_next
    rmmovq    %rax, 8(%rcx)     # *(curr + 1) = *curr
    rmmovq    %rsi, (%rcx)      # *curr = t
    jmp    inner_loop_next      # go to inner_loop_next
done:
    popq    %rbp
    ret                         # Return

# Stack starts here and grows to lower addresses
    .pos 0x200
stack:

```

Runing with yis:
```
Stopped in 133 steps at PC = 0x13.  Status 'HLT', CC Z=0 S=1 O=0
Changes to registers:
%rax:	0x0000000000000000	0x0000000000000001
%rcx:	0x0000000000000000	0x0000000000000020
%rdx:	0x0000000000000000	0x0000000000000020
%rsp:	0x0000000000000000	0x0000000000000200
%rsi:	0x0000000000000000	0x0000000000000002
%rdi:	0x0000000000000000	0x0000000000000018
%r8:	0x0000000000000000	0x0000000000000010
%r9:	0x0000000000000000	0xfffffffffffffff8
%r10:	0x0000000000000000	0x0000000000000008

Changes to memory:
0x0018:	0x0000000000000004	0x0000000000000001
0x0028:	0x0000000000000001	0x0000000000000003
0x0030:	0x0000000000000003	0x0000000000000004
0x01f0:	0x0000000000000000	0x0000000000000055
0x01f8:	0x0000000000000000	0x0000000000000013
```

we can see that:
- memory address 0x0018 was modified from 4 to 1
- memory address 0x0028 was modified from 1 to 3
- memory address 0x0030 was modified from 3 to 4

Our Y86-64 bubble sort works.